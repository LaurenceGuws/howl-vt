const std = @import("std");
const screen_capture = @import("../support/screen_capture.zig");
const screen_set = @import("../../src/screen_set.zig");
const selection = @import("../../src/selection/state.zig");
const terminal_mod = @import("../../src/terminal.zig");
const stream_harness = @import("../support/stream_harness.zig");

const Terminal = terminal_mod.Terminal;
const StreamHarness = stream_harness.Harness;

fn gridCellCount(rows: u16, cols: u16) u32 {
    return @as(u32, rows) * @as(u32, cols);
}

fn captureSnapshot(terminal: *const Terminal) !screen_capture.Capture {
    return screen_capture.Capture.captureFromScreen(
        terminal.allocator,
        terminal.screen_state.activeConst(),
        terminal.screen_state.activeSelectionConst().state(),
    );
}

fn visibleView(terminal: *const Terminal) screen_set.View {
    return screen_set.visibleView(&terminal.screen_state, 0);
}

test "snapshot: capture from simple text" {
    const gpa = std.testing.allocator;
    var terminal = try Terminal.initWithCells(gpa, 5, 10);
    defer terminal.deinit();
    var stream = try StreamHarness.init(&terminal);
    defer stream.deinit();

    try stream.nextSlice("HELLO");

    var snap = try captureSnapshot(&terminal);
    defer snap.deinit();

    try std.testing.expectEqual(@as(u16, 5), snap.rows);
    try std.testing.expectEqual(@as(u16, 10), snap.cols);
    try std.testing.expectEqual(@as(u16, 0), snap.cursor_row);
    try std.testing.expectEqual(@as(u16, 5), snap.cursor_col);
    try std.testing.expectEqual(true, snap.cursor_visible);
    try std.testing.expectEqual(true, snap.auto_wrap);
    try std.testing.expectEqual(@as(u21, 'H'), snap.cellAt(0, 0));
    try std.testing.expectEqual(@as(u21, 'E'), snap.cellAt(0, 1));
    try std.testing.expectEqual(@as(u21, 'L'), snap.cellAt(0, 2));
    try std.testing.expectEqual(@as(u21, 'L'), snap.cellAt(0, 3));
    try std.testing.expectEqual(@as(u21, 'O'), snap.cellAt(0, 4));
}

test "snapshot: determinism across identical state" {
    const gpa = std.testing.allocator;

    var vt_core1 = try Terminal.initWithCells(gpa, 5, 10);
    defer vt_core1.deinit();
    var stream1 = try StreamHarness.init(&vt_core1);
    defer stream1.deinit();
    try stream1.nextSlice("TEST");
    var snap1 = try captureSnapshot(&vt_core1);
    defer snap1.deinit();

    var vt_core2 = try Terminal.initWithCells(gpa, 5, 10);
    defer vt_core2.deinit();
    var stream2 = try StreamHarness.init(&vt_core2);
    defer stream2.deinit();
    try stream2.nextSlice("TEST");
    var snap2 = try captureSnapshot(&vt_core2);
    defer snap2.deinit();

    try std.testing.expectEqual(snap1.cursor_row, snap2.cursor_row);
    try std.testing.expectEqual(snap1.cursor_col, snap2.cursor_col);
    try std.testing.expectEqual(snap1.cursor_visible, snap2.cursor_visible);
    try std.testing.expectEqual(snap1.auto_wrap, snap2.auto_wrap);

    if (snap1.cells != null and snap2.cells != null) {
        const size = gridCellCount(snap1.rows, snap1.cols);
        try std.testing.expectEqualSlices(u21, snap1.cells.?[0..@intCast(size)], snap2.cells.?[0..@intCast(size)]);
    }
}

test "snapshot: split-feed equivalence" {
    const gpa = std.testing.allocator;

    var vt_core_atomic = try Terminal.initWithCells(gpa, 5, 10);
    defer vt_core_atomic.deinit();
    var atomic_stream = try StreamHarness.init(&vt_core_atomic);
    defer atomic_stream.deinit();
    try atomic_stream.nextSlice("ABCDEFGHIJ");
    var snap_atomic = try captureSnapshot(&vt_core_atomic);
    defer snap_atomic.deinit();

    var vt_core_chunked = try Terminal.initWithCells(gpa, 5, 10);
    defer vt_core_chunked.deinit();
    var chunked_stream = try StreamHarness.init(&vt_core_chunked);
    defer chunked_stream.deinit();
    try chunked_stream.next('A');
    try chunked_stream.next('B');
    try chunked_stream.nextSlice("CD");
    try chunked_stream.nextSlice("EFGHIJ");
    var snap_chunked = try captureSnapshot(&vt_core_chunked);
    defer snap_chunked.deinit();

    try std.testing.expectEqual(snap_atomic.cursor_col, snap_chunked.cursor_col);
    try std.testing.expectEqual(snap_atomic.cursor_row, snap_chunked.cursor_row);

    if (snap_atomic.cells != null and snap_chunked.cells != null) {
        const size = gridCellCount(snap_atomic.rows, snap_atomic.cols);
        try std.testing.expectEqualSlices(u21, snap_atomic.cells.?[0..@intCast(size)], snap_chunked.cells.?[0..@intCast(size)]);
    }
}

test "snapshot: history capture when history is enabled" {
    const gpa = std.testing.allocator;
    var terminal = try Terminal.initWithCellsAndHistory(gpa, 3, 5, 10);
    defer terminal.deinit();
    var stream = try StreamHarness.init(&terminal);
    defer stream.deinit();

    try stream.nextSlice("AAA\nBBB\nCCC\nDDD");

    var snap = try captureSnapshot(&terminal);
    defer snap.deinit();

    try std.testing.expectEqual(@as(u16, 3), snap.rows);
    try std.testing.expectEqual(@as(u16, 5), snap.cols);
    try std.testing.expectEqual(@as(u16, 10), snap.history_capacity);
    try std.testing.expectEqual(snap.history_count, visibleView(&terminal).history_count);

    if (snap.history != null) {
        try std.testing.expect(snap.history.?.len > 0);
    }
}

test "snapshot: historyRowAt matches terminal after wraparound" {
    const gpa = std.testing.allocator;
    var terminal = try Terminal.initWithCellsAndHistory(gpa, 2, 3, 2);
    defer terminal.deinit();
    var stream = try StreamHarness.init(&terminal);
    defer stream.deinit();

    // Force history ring-buffer wraparound (capacity 2, scroll more than 2 rows).
    try stream.nextSlice("111\n222\n333\n444\n555");

    var snap = try captureSnapshot(&terminal);
    defer snap.deinit();

    try std.testing.expectEqual(visibleView(&terminal).history_count, snap.history_count);
    try std.testing.expectEqual(screen_set.historyCapacity(&terminal.screen_state), snap.history_capacity);

    var idx: u32 = 0;
    while (idx < visibleView(&terminal).history_count) : (idx += 1) {
        var col: u16 = 0;
        while (col < terminal.screen_state.activeConst().cols) : (col += 1) {
            try std.testing.expectEqual(screen_set.historyRowAt(&terminal.screen_state, idx, col), snap.historyRowAt(idx, col));
        }
    }
}

test "snapshot: selection state is included" {
    const gpa = std.testing.allocator;
    var terminal = try Terminal.initWithCells(gpa, 5, 10);
    defer terminal.deinit();
    var stream = try StreamHarness.init(&terminal);
    defer stream.deinit();

    try stream.nextSlice("HELLO");

    selection.terminalStart(&terminal, 0, 0);
    selection.terminalUpdate(&terminal, 0, 4);
    selection.terminalFinish(&terminal);

    var snap = try captureSnapshot(&terminal);
    defer snap.deinit();

    try std.testing.expectEqual(true, snap.selection != null);
    if (snap.selection) |sel| {
        try std.testing.expectEqual(@as(i32, 0), sel.start.row);
        try std.testing.expectEqual(@as(u16, 0), sel.start.col);
        try std.testing.expectEqual(@as(i32, 0), sel.end.row);
        try std.testing.expectEqual(@as(u16, 4), sel.end.col);
        try std.testing.expectEqual(true, sel.active);
    }
}

test "snapshot: parity with direct screen state" {
    const gpa = std.testing.allocator;
    var terminal = try Terminal.initWithCells(gpa, 5, 10);
    defer terminal.deinit();
    var stream = try StreamHarness.init(&terminal);
    defer stream.deinit();

    try stream.nextSlice("TEST");

    var snap = try captureSnapshot(&terminal);
    defer snap.deinit();

    const screen = terminal.screen_state.activeConst();
    try std.testing.expectEqual(screen.rows, snap.rows);
    try std.testing.expectEqual(screen.cols, snap.cols);
    try std.testing.expectEqual(screen.cursor_row, snap.cursor_row);
    try std.testing.expectEqual(screen.cursor_col, snap.cursor_col);
    try std.testing.expectEqual(screen.cursor_visible, snap.cursor_visible);
    try std.testing.expectEqual(screen.auto_wrap, snap.auto_wrap);
}
